#include "../../ray.conf"
#include "../../ray_advanced.conf"
#include "../../shader/math.fxsub"
#include "../../shader/common.fxsub"
#include "../../shader/shadowcommon.fxsub"
#include "../../shader/gbuffer.fxsub"
#include "../../shader/gbuffer_sampler.fxsub"
#include "../../shader/PhaseFunctions.fxsub"

float mR : CONTROLOBJECT<string name="(self)"; string item="R+";>;
float mG : CONTROLOBJECT<string name="(self)"; string item="G+";>;
float mB : CONTROLOBJECT<string name="(self)"; string item="B+";>;
float mRangeP : CONTROLOBJECT<string name="(self)"; string item="Range+";>;
float mRangeM : CONTROLOBJECT<string name="(self)"; string item="Range-";>;
float mIntensityP : CONTROLOBJECT<string name="(self)"; string item="Intensity+";>;
float mIntensityM : CONTROLOBJECT<string name="(self)"; string item="Intensity-";>;
float mMiePhaseP : CONTROLOBJECT<string name="(self)"; string item="MiePhase+";>;
float mMiePhaseM : CONTROLOBJECT<string name="(self)"; string item="MiePhase-";>;
float mMieDensityP : CONTROLOBJECT<string name="(self)"; string item="Density+";>;
float mMieDensityM : CONTROLOBJECT<string name="(self)"; string item="Density-";>;
float mWidthP : CONTROLOBJECT<string name="(self)"; string item="Width+";>;
float mHeightP : CONTROLOBJECT<string name="(self)"; string item="Height+";>;
float mDepthP : CONTROLOBJECT<string name="(self)"; string item="Depth+";>;

float3 mPosition : CONTROLOBJECT<string name="(self)"; string item = "Position";>;

static const float FogWidth = lerp(FogRangeParams.x, FogRangeParams.z, mWidthP);
static const float FogHeight = lerp(FogRangeParams.x, FogRangeParams.z, mHeightP);
static const float FogDepth = lerp(FogRangeParams.x, FogRangeParams.z, mDepthP);
static const float FogMiePhase  = lerp(lerp(FogMieParams.x, FogMieParams.z, mMiePhaseP), FogMieParams.y, mMiePhaseM);
static const float FogDensity = lerp(lerp(FogDensityParams.x, FogDensityParams.z, mMieDensityP), FogDensityParams.y, mMieDensityM);
static const float FogIntensity = lerp(lerp(FogIntensityParams.x, FogIntensityParams.z, mIntensityP), FogIntensityParams.y, mIntensityM);

static const float3 FogPosition = mPosition;
static const float3 FogColor = float3(mR, mG, mB) * FogIntensity;

struct FogParams
{
	float3 FogColor;
	float3 FogPosition;
	float3 FogRange;

	float FogDensity;
	float FogIntensity;
	float FogMiePhase;
};

float2 ComputeRayCubeIntersection(float3 p, float3 d, float3 q, float3 symbol, float3 size)
{
	const float3 inv = 1.0 / (d + 1e-16);
	const float3 aabb[2] = { q-size, q + size };

	float tmin = 65535;
	float tmax = -65535;

	float tymin, tymax, tzmin, tzmax;
	tmin  = (aabb[1-symbol[0]].x - p.x) * inv.x;
	tmax  = (aabb[  symbol[0]].x - p.x) * inv.x;
	tymin = (aabb[1-symbol[1]].y - p.y) * inv.y;
	tymax = (aabb[  symbol[1]].y - p.y) * inv.y;
	tzmin = (aabb[1-symbol[2]].z - p.z) * inv.z;
	tzmax = (aabb[  symbol[2]].z - p.z) * inv.z;

	tmin = max(max(tmin, tymin), tzmin);
	tmax = min(min(tmax, tymax), tzmax);

	if (tmin > tmax)
		return -1;

	return float2(tmin, tmax);
}

float ComputeOpticalDepth(FogParams param, float depth)
{
	return exp(-depth * param.FogDensity);
}

float2 hash( in float2 x )
{
    const float2 k = float2( 0.3183099, 0.3678794 );
    x = x*k + k.yx;
    float h = frac(16.0 * k.x*frac( x.x*x.y*(x.x+x.y)));
    
    float a = 6.2831*h + time;
    return float2( cos(a), sin(a) );
}

float3 noised(in float2 p)
{
    float2 i = floor( p );
    float2 f = frac( p );

    float2 u = f*f*f*(f*(f*6.0-15.0)+10.0);
    float2 du = 30.0*f*f*(f*(f-2.0)+1.0);
    
    float2 ga = hash(i + float2(0.0,0.0));
    float2 gb = hash(i + float2(1.0,0.0));
    float2 gc = hash(i + float2(0.0,1.0));
    float2 gd = hash(i + float2(1.0,1.0));
    
    float va = dot( ga, f - float2(0.0,0.0) );
    float vb = dot( gb, f - float2(1.0,0.0) );
    float vc = dot( gc, f - float2(0.0,1.0) );
    float vd = dot( gd, f - float2(1.0,1.0) );

    return float3( va + u.x*(vb-va) + u.y*(vc-va) + u.x*u.y*(va-vb-vc+vd), ga + u.x*(gb-ga) + u.y*(gc-ga) + u.x*u.y*(ga-gb-gc+gd) + du * (u.yx*(va-vb-vc+vd) + float2(vb,vc) - va));
}

float displacementSimple(float2 p, float time, float freq = 0.1)
{
    return noised(p * freq).x;
}

float4 VolumetricFog(FogParams param, float3 V, float3 L, float3 start, float3 end, float jitter, int stepNum = VOLUMETRIC_FOG_SAMPLES_LENGTH)
{
	float3 sampleStep = (end - start) / stepNum;
	float3 sampleStart = start + sampleStep * jitter;

	float sampleLength = length(sampleStep);
	float sampleOpticalDepth = ComputeOpticalDepth(param, sampleLength);

	float scatteringMie = 0;
	float scatteringDepth = 1;
	float scatteringDensity = 0;

	float3 symbol = step(0, L);

	for (int i = 0; i < stepNum; i++, sampleStart += sampleStep, scatteringDepth *= sampleOpticalDepth)
	{
		float height = displacementSimple(sampleStart.xz, time) * param.FogRange.y + param.FogPosition.y;
		if (height > sampleStart.y)
		{
			float2 intersections = ComputeRayCubeIntersection(sampleStart, L, param.FogPosition,  symbol, param.FogRange);
			float3 sampleLightPoint = sampleStart + L * intersections.y;

			float scattering = exp(-distance(sampleLightPoint, sampleStart) * param.FogDensity);
			scattering *= sampleOpticalDepth;
			scattering *= scatteringDepth;
			scattering *= sampleLength;

			scatteringMie += scattering;
			scatteringDensity += sampleLength;
		}
	}

	float3 scattering = param.FogColor * SunColor * scatteringMie;
	scattering *= param.FogIntensity;
#if VOLUMETRIC_FOG_ANISOTROPY
	scattering *= ComputePhaseMieHG(dot(V, L), param.FogMiePhase);
#endif

	return float4(scattering, scatteringDensity * param.FogDensity * param.FogIntensity);
}

void VolumetricCubeVS(
	in float4 Position : POSITION,
	in float3 Normal : NORMAL,
	out float4 oTexcoord0 : TEXCOORD0,
	out float3 oTexcoord1 : TEXCOORD1,
	out float4 oPosition  : POSITION)
{
	oTexcoord0 = oPosition = mul(Position, matViewProject);
	oTexcoord0.xy = PosToCoord(oTexcoord0.xy / oTexcoord0.w) + ViewportOffset;
	oTexcoord0.xy = oTexcoord0.xy * oTexcoord0.w;
	oTexcoord1 = Position.xyz - CameraPosition;
}

float4 VolumetricCubePS(float4 texcoord : TEXCOORD0, float3 viewdir : TEXCOORD1) : COLOR
{
	FogParams param;
	param.FogPosition = FogPosition;
	param.FogRange = float3(FogWidth, FogHeight, FogDepth);
	param.FogIntensity = FogIntensity;
	param.FogColor = FogColor;
	param.FogMiePhase = FogMiePhase;
	param.FogDensity = FogDensity;

	float3 V = normalize(viewdir);
	float2 intersections = ComputeRayCubeIntersection(CameraPosition, V, param.FogPosition, step(0, V), param.FogRange);
	clip(intersections.y);

	texcoord.xy /= texcoord.w;

	float linearDepth = tex2Dlod(Gbuffer8Map, float4(texcoord.xy, 0, 0)).r;
	clip(linearDepth - intersections.x);

	float3 start = CameraPosition + V * max(0, intersections.x);
	float3 end = CameraPosition + V * min(linearDepth, intersections.y);

	return VolumetricFog(param, V, normalize(-SunDirection), start, end, GetJitterOffset(texcoord * ViewportSize + time));
}

technique MainTechBS0<string MMDPass = "object_ss"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = CW;
		VertexShader = compile vs_3_0 VolumetricCubeVS();
		PixelShader = compile ps_3_0 VolumetricCubePS();
	}
}

technique DepthTech<string MMDPass = "object";>{}
technique EdgeTec<string MMDPass = "edge";>{}
technique ShadowTech<string MMDPass = "shadow";>{}
technique ZplotTec<string MMDPass = "zplot";>{}