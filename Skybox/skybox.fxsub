#include "../ray.conf"
#include "../shader/math.fx"
#include "../shader/common.fx"

#ifndef SKYBOX_MAX_SIZE
#   define SKYBOX_MAX_SIZE 1000
#endif

#ifndef SKYDOME_ENABLE
#   define SKYDOME_ENABLE 0
#endif

shared texture2D ShadowmapMap : RENDERCOLORTARGET;
sampler ShadowmapSamp = sampler_state {
    texture = <ShadowmapMap>;
    MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = LINEAR;
    AddressU  = CLAMP;  AddressV = CLAMP;
};

bool ExistRay : CONTROLOBJECT<string name = "ray.x";>;
float mScale : CONTROLOBJECT < string name="(self)"; string item = "Scale+"; >;

float EnvRotateX : CONTROLOBJECT < string name="ray_controller.pmx"; string item = "EnvRotateX"; >;
float EnvRotateY : CONTROLOBJECT < string name="ray_controller.pmx"; string item = "EnvRotateY"; >;
float EnvRotateZ : CONTROLOBJECT < string name="ray_controller.pmx"; string item = "EnvRotateZ"; >;

static float rotateX = EnvRotateX * PI_2;
static float rotateY = EnvRotateY * PI_2;
static float rotateZ = EnvRotateZ * PI_2;
static float3x3 rotate = makeRotate(rotateX, rotateY, rotateZ);

texture decal : MATERIALTEXTURE;
sampler decalMap = sampler_state
{
    texture = <decal>;
    MINFILTER = LINEAR;
    MAGFILTER = LINEAR;
    MIPFILTER = NONE;
    ADDRESSU  = CLAMP;
    ADDRESSV  = CLAMP;
};

void SkyboxVS(
    in float4 Position : POSITION,
    out float4 oNormal   : TEXCOORD0,
    out float4 oPosition : SV_Position)
{
#if SKYDOME_ENABLE
    oNormal = normalize(Position);
    oNormal.y = oNormal.y < -0.05 ? -0.05 : oNormal.y;
    oNormal.y += 0.04999;
    oPosition = mul(float4(oNormal.xyz * lerp(SKYBOX_MIN_SIZE, SKYBOX_MAX_SIZE, mScale), 1) , matViewProject);
    oNormal.y -= 0.04999;
#else
    oNormal = normalize(Position);
    oPosition = mul(float4(oNormal.xyz * lerp(SKYBOX_MIN_SIZE, SKYBOX_MAX_SIZE, mScale), 1), matViewProject);
#endif
}

float4 SkyboxPS(in float4 normal : TEXCOORD0, in float4 screenPosition : SV_Position) : COLOR
{
    float3 N = normalize(mul(rotate, normal.xyz));   
    if (ExistRay)
    {
        float4 color = tex2D(decalMap, computeSphereCoord(N));
    #if SKYBOX_HDR_ENABLE
        color = float4(DecodeRGBM(color), 1);
    #endif

    #if SKYDOME_ENABLE && SHADOW_QUALITY > 0
        color *= lerp(1, tex2D(ShadowmapSamp, float2(screenPosition.xy) / ViewportSize + ViewportOffset).r, 0.9);
    #endif
    
    #if SKYBOX_GAMMA_CORRECTION > 0
        return color;
    #else
        return linear2srgb(color);
    #endif
    }
    else
    {
#if SKYBOX_HDR_ENABLE
        float4 rgbm = tex2D(decalMap, computeSphereCoord(N));
        return float4(linear2srgb(DecodeRGBM(rgbm)), 1);
#else
        return tex2D(decalMap, computeSphereCoord(N));
#endif
    }
}

#define OBJECT_TEC(name, mmdpass) \
    technique name < string MMDPass = mmdpass; \
    string Script = \
        "RenderColorTarget0=;" \
        "RenderDepthStencilTarget=;" \
        "Pass=DrawObject;" \
    ; \
    > { \
        pass DrawObject { \
            AlphaTestEnable = FALSE; AlphaBlendEnable = FALSE; \
            VertexShader = compile vs_3_0 SkyboxVS(); \
            PixelShader  = compile ps_3_0 SkyboxPS(); \
        } \
    }

OBJECT_TEC(MainTec0, "object")
OBJECT_TEC(MainTec1, "object_ss")

technique EdgeTec < string MMDPass = "edge"; > {}
technique ShadowTec < string MMDPass = "shadow"; > {}
technique ZplotTec < string MMDPass = "zplot"; > {}