#include "../../../ray.conf"
#include "../../../ray_advanced.conf"
#include "../../../shader/math.fxsub"
#include "../../../shader/common.fxsub"
#include "../../../shader/gbuffer.fxsub"
#include "../../../shader/gbuffer_sampler.fxsub"
#include "../../../shader/lighting.fxsub"
#include "../../../shader/arealight.fxsub"

#if SHADOW_MAP_ENABLE
#	include "../../../shader/shadowcommon.fxsub"
#endif

#define RECTANGLELIGHT_MAX_WIDTH 50
#define RECTANGLELIGHT_MAX_HEIGHT 50

float mR : CONTROLOBJECT < string name="(self)"; string item = "R+"; >;
float mG : CONTROLOBJECT < string name="(self)"; string item = "G+"; >;
float mB : CONTROLOBJECT < string name="(self)"; string item = "B+"; >;
float mBlink : CONTROLOBJECT < string name="(self)"; string item = "Blink"; >;
float mRangeP : CONTROLOBJECT < string name="(self)"; string item = "Range+"; >;
float mWidthP : CONTROLOBJECT < string name="(self)"; string item = "Width+"; >;
float mHeightP : CONTROLOBJECT < string name="(self)"; string item = "Height+"; >;
float mHardnessP : CONTROLOBJECT<string name="(self)"; string item = "Hardness";>;
float mIntensityP : CONTROLOBJECT < string name="(self)"; string item = "Intensity+"; >;
float mIntensityM : CONTROLOBJECT < string name="(self)"; string item = "Intensity-"; >;
float mAttenuationBulbP : CONTROLOBJECT < string name="(self)"; string item = "AttenuationBulb+"; >;
float mMultiLightP : CONTROLOBJECT < string name="ray_controller.pmx"; string item = "MultiLight+"; >;
float mMultiLightM : CONTROLOBJECT < string name="ray_controller.pmx"; string item = "MultiLight-"; >;

float3 mPosition : CONTROLOBJECT < string name="(self)"; string item = "Position"; >;
float3 mDirection : CONTROLOBJECT < string name="(self)"; string item = "Direction"; >;
float3 mRightTop : CONTROLOBJECT < string name="(self)"; string item = "RightTop"; >;
float3 mRightBottom : CONTROLOBJECT < string name="(self)"; string item = "RightBottom"; >;

static float LightHardness = lerp(0.15, 0.5, mHardnessP);
static float LightRange = lerp(1, LIGHTSOURCE_MAX_RANGE, mRangeP);
static float LightWidth = lerp(1, RECTANGLELIGHT_MAX_WIDTH, mWidthP);
static float LightHeight = lerp(1, RECTANGLELIGHT_MAX_HEIGHT, mHeightP);
static float LightAttenuationBulb = lerp(1, mLightAttenuationBulbMax, mAttenuationBulbP);
static float LightIntensity = lerp(lerp(LIGHTSOURCE_MIN_INTENSITY, LIGHTSOURCE_MAX_INTENSITY, mIntensityP), 0, mIntensityM);
static float LightIntensity2 = lerp(lerp(mLightIntensityMin, mLightIntensityMax, mMultiLightP), 0, mMultiLightM);

static float3 LightPosition = mPosition;
static float3 LightDirection = normalize(mDirection - mPosition);
static float2 LightRect = float2(LightWidth, LightHeight);
static float3 LightColor = float3(mR, mG, mB) * saturate(1 - SmoothTriangleWave(time * (mBlink * 10)));

static float3 viewLightPosition = mul(float4(LightPosition, 1), matView).xyz;
static float3 viewLightDirection = normalize(mul(LightDirection, (float3x3)matView));
static float3 viewLightRight = normalize(mul((mRightTop + mRightBottom) / 2 - LightPosition, (float3x3)matView));
static float3 viewLightUp = normalize(cross(viewLightDirection, viewLightRight));

#if SHADOW_MAP_ENABLE
	static float4x4 matLightProject = CreatePerspectiveProject(60.0f / 180 * PI, 1, mLightPlaneNear, mLightPlaneFar);
	static float4x4 matLightView = CreateLightViewMatrix(LightDirection, LightPosition);
	static float4x4 matLightViewProject = mul(matLightView, matLightProject);
	static float4x4 matLightViewFromCameraView = mul(matViewInverse, matLightView);
	static float4x4 matLightProjectFromCameraView = mul(matViewInverse, matLightViewProject);
#endif

#if VIDEO_MAP_ENABLE && VIDEO_MAP_IN_TEXTURE != 1
	#if VIDEO_MAP_IN_SCREEN_MAP
		shared texture2D DummyScreenTex : RenderColorTarget;
	#elif VIDEO_MAP_ANIMATION_ENABLE
		texture VideoMap : ANIMATEDTEXTURE <string ResourceName = VIDEO_MAP_FILE; float speed=VIDEO_MAP_ANIMATION_SPEED;>;
	#else
		texture VideoMap<string ResourceName = VIDEO_MAP_FILE;>;
	#endif
	sampler VideoMapSamp = sampler_state
	{
#if VIDEO_MAP_IN_SCREEN_MAP
		texture = DummyScreenTex;
#else
		texture = VideoMap;
#endif
		MINFILTER = LINEAR; MAGFILTER = LINEAR; MipFilter = LINEAR; ADDRESSU = WRAP; ADDRESSV = WRAP;
	};
#elif VIDEO_MAP_ENABLE
	texture VideoMap: MATERIALTEXTURE;
	sampler VideoMapSamp = sampler_state
	{
		texture = <VideoMap>;
		MINFILTER = LINEAR; MAGFILTER = LINEAR; MipFilter = LINEAR; ADDRESSU = WRAP; ADDRESSV = WRAP;
	};
#endif

#if SHADOW_MAP_ENABLE

#if SHADOW_MAP_QUALITY == 0
#   define SHADOW_MAP_SIZE 256
#elif SHADOW_MAP_QUALITY == 1
#   define SHADOW_MAP_SIZE 512
#elif SHADOW_MAP_QUALITY >= 2
#   define SHADOW_MAP_SIZE 1024
#elif SHADOW_MAP_QUALITY >= 3
#   define SHADOW_MAP_SIZE 2048
#else
#   define SHADOW_MAP_SIZE 256
#endif

texture ShadowMap: OFFSCREENRENDERTARGET <
	string Description = "Shadow Rendering for SpotLight";
	int Width = SHADOW_MAP_SIZE;
	int Height = SHADOW_MAP_SIZE;
	string Format = "G32R32F";
	float4 ClearColor = { 0, 0, 0, 0 };
	float ClearDepth = 1.0;
	int MipLevels = 1;
	string DefaultEffect = 
		"self = hide;"
		"*fog.pmx=hide;"
		"*controller.pmx=hide;"
		"*editor*.pmx=hide;"
		"*.pmx = ../rectangle_shadow.fx;"
		"*.pmd = ../rectangle_shadow.fx;"
		"* = hide";
>;
sampler ShadowMapSamp = sampler_state {
	texture = <ShadowMap>;
	MinFilter = LINEAR; MagFilter = LINEAR; MipFilter = NONE;
	AddressU = CLAMP; AddressV = CLAMP;
};

static float2 ShadowmapOffset  = 0.5 / SHADOW_MAP_SIZE;
static float2 ShadowmapOffset2 = 1.0 / SHADOW_MAP_SIZE;

float ShadowPCF(float3 viewPosition, int samples) 
{
	float4 proj = mul(float4(viewPosition, 1), matLightProjectFromCameraView);
	proj.xy = PosToCoord(proj.xy / proj.w);
	proj.xy += ShadowmapOffset;

#if SHADOW_MAP_SOFT_QUALITY > 0
	float2 shadow = 0.0;
	float shadowWeight = 0.0;

	static const float width = 7;
	float radius = (width - 1) / 2;
	float kernel[width];

	float3 incrementalGaussian = IncrementalGaussianCofficent(width);

	[unroll]
	for (int i = 0; i <= radius; ++i)
	{
		incrementalGaussian = IncrementalGaussian(incrementalGaussian);
		kernel[radius - i] = incrementalGaussian.x;
		kernel[radius + i] = incrementalGaussian.x;
	}

	[unroll]
	for (int i = 0; i < width; ++i)
	{
		shadowWeight += kernel[i];
	}

	[unroll]
	for (int x = -radius; x <= radius; x++) 
	{
		[unroll]
		for (int y = -radius; y <= radius; y++)
		{
			float2 pos = proj.xy + float2(x, y) * ShadowmapOffset2;
			float weight = kernel[radius+x] * kernel[radius+y];
			shadow += tex2Dlod(ShadowMapSamp, float4(pos, 0, 0)).rg * weight;
		}
	}

	shadow = shadow / (shadowWeight * shadowWeight);

	float linearDepth = dot(float4(viewPosition,1), matLightViewFromCameraView._13_23_33_43);
	return ChebyshevUpperBound(shadow, linearDepth, 0.1, LightHardness);
#else
	float2 shadow = tex2Dlod(ShadowMapSamp, float4(proj.xy, 0, 0)).rg;
	float linearDepth = dot(float4(viewPosition,1), matLightViewFromCameraView._13_23_33_43);
	return ChebyshevUpperBound(shadow, linearDepth, 0.1, LightHardness);
#endif
}

float ShadingShadow(MaterialParam material, float3 V)
{
	float angle = saturate(dot(material.normal, -viewLightDirection));
	if (angle < 0.0)
	{
		clip(-1);
	}

	float3 P = V * material.linearDepth / V.z;
	float shadow = ShadowPCF(P, 0);
	return shadow;
}

#endif

float4 FastBlur(sampler source, float2 uv, float2 offset, float level)
{
	float sz = pow(2.0, level);
	float mip = level + 1.0;

	float4 a = tex2Dlod(source, float4(uv + sz * float2(-1.5, -0.5) * offset, 0, mip));
	float4 b = tex2Dlod(source, float4(uv + sz * float2( 0.5, -1.5) * offset, 0, mip));
	float4 c = tex2Dlod(source, float4(uv + sz * float2( 1.5,  0.5) * offset, 0, mip));
	float4 d = tex2Dlod(source, float4(uv + sz * float2(-0.5,  1.5) * offset, 0, mip));
	float4 e = tex2Dlod(source, float4(uv, 0, mip));

	float gamma = 2.0 + 1.0 / 3.0;

    float4 blurred = 0;
    blurred += pow(a, gamma) * 4.0;
	blurred += pow(b, gamma) * 4.0;
	blurred += pow(c, gamma) * 4.0;
	blurred += pow(d, gamma) * 4.0;
	blurred += pow(e, gamma);
    blurred /= 17;
    
    return blurred;
}

void ShadingMaterial(MaterialParam material, float3 V, out float3 diffuse, out float3 specular)
{
	float3 P = V * material.linearDepth / V.z;

	float2 coord1, coord2;
	float3 L = viewLightPosition - P;
	float3 Lw = RectangleDirection(L, viewLightRight, viewLightUp, viewLightDirection, LightRect, coord1);
	float3 L2 = normalize(Lw);
	float atten = GetPhysicalLightAttenuation(Lw, LightRange, LightAttenuationBulb);
	atten *= RectangleAttenuation(Lw, viewLightDirection, PI / 2, LightRange);

	float roughness = max(SmoothnessToRoughness(material.smoothness), 0.001);

	float4 energy = RectangleLightEnergy(material, material.normal, L, V, viewLightRight, viewLightUp, viewLightDirection, LightRect, roughness, material.specular, coord2);
	diffuse = energy.r + SubsurfaceShading(material, material.normal, L2, V).rgb;
	diffuse *= atten * LightColor * LightIntensity * LightIntensity2;
	
#if LIGHTSOURCE_DIFFUSE_ONLY
	specular = 0.0;
#else
	specular = energy.gba;
	specular *= atten * LightColor * LightIntensity * LightIntensity2;
#endif

#if VIDEO_MAP_ENABLE
	#if VIDEO_MAP_UV_FLIP
		coord1.x = 1 - coord1.x;
		coord2.x = 1 - coord2.x;
	#endif

	float mip = (1 - material.smoothness * material.smoothness) * 4;

	float3 prefilterDiffuse = FastBlur(VideoMapSamp, coord1, ViewportOffset2, 2).rgb;
	diffuse *= prefilterDiffuse;
	
#if LIGHTSOURCE_DIFFUSE_ONLY == 0
	float3 prefilterSpecular = FastBlur(VideoMapSamp, coord2, ViewportOffset2, mip).rgb;
	specular *= prefilterSpecular;
#endif
#endif
}

void LightSourceVS(
	in float4 Position : POSITION,
	in float3 Normal : NORMAL,
	in float2 Texcoord : TEXCOORD0,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oTexcoord1 : TEXCOORD1,
	out float4 oPosition  : POSITION)
{   
	oPosition = float4(CoordToPos(Texcoord), 0, 1);
	oTexcoord0 = Texcoord.xyxy + ViewportOffset.xyxy;
	oTexcoord1 = -mul(oPosition, matProjectInverse);
}

void LightSourcePS(
	float2 coord : TEXCOORD0, 
	float3 viewdir : TEXCOORD1,
	out float4 oColor0 : COLOR0,
	out float4 oColor1 : COLOR1,
	out float4 oColor2 : COLOR2)
{
	float4 MRT1 = tex2Dlod(Gbuffer1Map, float4(coord, 0, 0));
	float4 MRT2 = tex2Dlod(Gbuffer2Map, float4(coord, 0, 0));
	float4 MRT3 = tex2Dlod(Gbuffer3Map, float4(coord, 0, 0));
	float4 MRT4 = tex2Dlod(Gbuffer4Map, float4(coord, 0, 0));

	MaterialParam material;
	DecodeGbuffer(MRT1, MRT2, MRT3, MRT4, material);

	float4 MRT5 = tex2Dlod(Gbuffer5Map, float4(coord, 0, 0));
	float4 MRT6 = tex2Dlod(Gbuffer6Map, float4(coord, 0, 0));
	float4 MRT7 = tex2Dlod(Gbuffer7Map, float4(coord, 0, 0));
	float4 MRT8 = tex2Dlod(Gbuffer8Map, float4(coord, 0, 0));

	MaterialParam materialAlpha;
	DecodeGbuffer(MRT5, MRT6, MRT7, MRT8, materialAlpha);

	clip(sum(materialAlpha.albedo + materialAlpha.specular) - 1e-5);

	float3 V = normalize(viewdir);

	float3 diffuse, specular;
	ShadingMaterial(material, V, diffuse, specular);

	float3 diffuseAlpha, specularAlpha;
	ShadingMaterial(materialAlpha, V, diffuseAlpha, specularAlpha);

#if SSSS_QUALITY
	oColor0 = float4(diffuse, 0);
#else
	oColor0 = float4(diffuse * material.albedo, 0);
#endif
	oColor1 = float4(specular, 0);
	oColor2 = float4(diffuseAlpha * materialAlpha.albedo + specularAlpha, 0);

#if SHADOW_MAP_ENABLE
	clip(sum(oColor0 + oColor1 + oColor2) - 1e-5);

	float shadow = ShadingShadow(materialAlpha, V);
	oColor0 *= shadow;
	oColor1 *= shadow;
	oColor2 *= shadow;
#endif
}

void LightSourceShadowlessPS(
	float2 coord : TEXCOORD0, 
	float3 viewdir : TEXCOORD1,
	out float4 oColor0 : COLOR0,
	out float4 oColor1 : COLOR1,
	out float4 oColor2 : COLOR2)
{
	float4 MRT1 = tex2Dlod(Gbuffer1Map, float4(coord, 0, 0));
	float4 MRT2 = tex2Dlod(Gbuffer2Map, float4(coord, 0, 0));
	float4 MRT3 = tex2Dlod(Gbuffer3Map, float4(coord, 0, 0));
	float4 MRT4 = tex2Dlod(Gbuffer4Map, float4(coord, 0, 0));

	MaterialParam material;
	DecodeGbuffer(MRT1, MRT2, MRT3, MRT4, material);

	float4 MRT5 = tex2Dlod(Gbuffer5Map, float4(coord, 0, 0));
	float4 MRT6 = tex2Dlod(Gbuffer6Map, float4(coord, 0, 0));
	float4 MRT7 = tex2Dlod(Gbuffer7Map, float4(coord, 0, 0));
	float4 MRT8 = tex2Dlod(Gbuffer8Map, float4(coord, 0, 0));

	MaterialParam materialAlpha;
	DecodeGbuffer(MRT5, MRT6, MRT7, MRT8, materialAlpha);

	clip(sum(materialAlpha.albedo + materialAlpha.specular) - 1e-5);

	float3 V = normalize(viewdir);

	float3 diffuse, specular;
	ShadingMaterial(material, V, diffuse, specular);

	float3 diffuseAlpha, specularAlpha;
	ShadingMaterial(materialAlpha, V, diffuseAlpha, specularAlpha);

#if SSSS_QUALITY
	oColor0 = float4(diffuse, 0);
#else
	oColor0 = float4(diffuse * material.albedo, 0);
#endif
	oColor1 = float4(specular, 0);
	oColor2 = float4(diffuseAlpha * materialAlpha.albedo + specularAlpha, 0);
}

technique MainTech0 < string MMDPass = "object";
	string Script = 
		"RenderColorTarget0=;"
		"RenderColorTarget1=LightSpecMap;"
		"RenderColorTarget2=LightAlphaMap;"
		"Pass=DrawObject;"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = NONE;
		VertexShader = compile vs_3_0 LightSourceVS();
		PixelShader  = compile ps_3_0 LightSourceShadowlessPS();
	}
}

technique MainTecBS0 < string MMDPass = "object_ss";
	string Script = 
		"RenderColorTarget0=;"
		"RenderColorTarget1=LightSpecMap;"
		"RenderColorTarget2=LightAlphaMap;"
		"Pass=DrawObject;"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = NONE;
		VertexShader = compile vs_3_0 LightSourceVS();
		PixelShader  = compile ps_3_0 LightSourcePS();
	}
}

technique EdgeTec < string MMDPass = "edge"; > {}
technique ShadowTech < string MMDPass = "shadow";  > {}
technique ZplotTec < string MMDPass = "zplot"; > {}