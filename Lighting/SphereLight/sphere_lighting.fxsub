#include "../../../ray.conf"
#include "../../../ray_advanced.conf"
#include "../../../shader/math.fxsub"
#include "../../../shader/common.fxsub"
#include "../../../shader/gbuffer.fxsub"
#include "../../../shader/gbuffer_sampler.fxsub"
#include "../../../shader/lighting.fxsub"
#include "../../../shader/arealight.fxsub"

#if SHADOW_MAP_ENABLE
#	include "../../../shader/shadowcommon.fxsub"
#endif

#define SPHERELIGHT_MAX_SIZE 25

float mR : CONTROLOBJECT<string name="(self)"; string item = "R+";>;
float mG : CONTROLOBJECT<string name="(self)"; string item = "G+";>;
float mB : CONTROLOBJECT<string name="(self)"; string item = "B+";>;
float mBlink : CONTROLOBJECT<string name="(self)"; string item = "Blink";>;
float mSizeP : CONTROLOBJECT<string name="(self)"; string item = "Size+";>;
float mRangeP : CONTROLOBJECT<string name="(self)"; string item = "Range+";>;
float mHardnessP : CONTROLOBJECT<string name="(self)"; string item = "Hardness";>;
float mIntensityP : CONTROLOBJECT<string name="(self)"; string item = "Intensity+";>;
float mIntensityM : CONTROLOBJECT<string name="(self)"; string item = "Intensity-";>;
float mAttenuationBulbP : CONTROLOBJECT<string name="(self)"; string item = "AttenuationBulb+";>;
float mMultiLightP : CONTROLOBJECT<string name="ray_controller.pmx"; string item = "MultiLight+";>;
float mMultiLightM : CONTROLOBJECT<string name="ray_controller.pmx"; string item = "MultiLight-";>;

float3 mPosition : CONTROLOBJECT<string name="(self)"; string item = "Position";>;
float3 mDirection : CONTROLOBJECT<string name="(self)"; string item = "Direction";>;

static float LightHardness = lerp(0.2, 0.995, mHardnessP);
static float LightSize = (1 + mSizeP * SPHERELIGHT_MAX_SIZE);
static float LightBlink = saturate(1 - SmoothTriangleWave(time * (mBlink * 10)));
static float LightRange = (1 + (mSizeP * SPHERELIGHT_MAX_SIZE + mRangeP * LIGHTSOURCE_MAX_RANGE));
static float LightIntensity = lerp(lerp(LIGHTSOURCE_MIN_INTENSITY, LIGHTSOURCE_MAX_INTENSITY, mIntensityP), 0, mIntensityM);
static float LightIntensity2 = lerp(lerp(mLightIntensityMin, mLightIntensityMax, mMultiLightP), 0, mMultiLightM);
static float LightAttenuationBulb = lerp(1, mLightAttenuationBulbMax, mAttenuationBulbP);

static float3 LightPosition = mPosition;
static float3 LightDirection = normalize(mDirection - mPosition);
static float3 LightColor = float3(mR, mG, mB) * LightIntensity * LightIntensity2 * LightBlink;

static float3 viewLightPosition = mul(float4(LightPosition, 1), matView).xyz;

#if IBL_ENABLE
texture IBLDiffuseTexture<string ResourceName = IBLDIFF_MAP_FILE; int Miplevels = 1;>; 
sampler IBLDiffuseSampler = sampler_state {
	texture = <IBLDiffuseTexture>;	
	MINFILTER = LINEAR; MAGFILTER = LINEAR; MIPFILTER = NONE;	
	ADDRESSU = CLAMP; ADDRESSV = CLAMP;
};

texture IBLSpecularTexture<string ResourceName = IBLSPEC_MAP_FILE; int Miplevels = IBL_MIPMAP_LEVEL;>; 
sampler IBLSpecularSampler = sampler_state {
	texture = <IBLSpecularTexture>;
	MINFILTER = LINEAR; MAGFILTER = LINEAR; MIPFILTER = LINEAR;	
	ADDRESSU = CLAMP; ADDRESSV = CLAMP;
};

float3 DecodeHDR(float4 rgbx)
{
#if IBL_RGBT_RANGE
	return DecodeRGBT(rgbx, IBL_RGBT_RANGE);
#elif IBL_RGBM_RANGE
	return DecodeRGBM(rgbx, IBL_RGBM_RANGE);
#else
	return DecodeRGBM(rgbx, 6);
#endif
}
#endif

#if SHADOW_MAP_ENABLE

#if SHADOW_MAP_QUALITY == 0
#   define SHADOW_MAP_WIDTH 256
#   define SHADOW_MAP_HEIGHT 512
#elif SHADOW_MAP_QUALITY == 1
#   define SHADOW_MAP_WIDTH 512
#   define SHADOW_MAP_HEIGHT 1024
#elif SHADOW_MAP_QUALITY >= 2
#   define SHADOW_MAP_WIDTH 1024
#   define SHADOW_MAP_HEIGHT 2048
#elif SHADOW_MAP_QUALITY >= 3
#   define SHADOW_MAP_WIDTH 2048
#   define SHADOW_MAP_HEIGHT 4096
#else
#   define SHADOW_MAP_WIDTH 256
#   define SHADOW_MAP_HEIGHT 512
#endif

texture ShadowMap: OFFSCREENRENDERTARGET <
	string Description = "Shadow Rendering for SphereLight";
	int2 Dimensions = {SHADOW_MAP_WIDTH, SHADOW_MAP_HEIGHT};
	string Format = "G32R32F";
	float4 ClearColor = { 1, 1, 1, 1 };
	float ClearDepth = 1.0;
	string DefaultEffect = 
		"self = hide;"
		"*fog.pmx=hide;"
		"*controller.pmx=hide;"
		"*editor*.pmx=hide;"
		"*.pmx = ../sphere_shadow.fx;"
		"*.pmd = ../sphere_shadow.fx;"
		"* = hide";
>;
sampler ShadowMapSamp = sampler_state {
	texture = <ShadowMap>;
	MinFilter = POINT; MagFilter = LINEAR; MipFilter = NONE;
	AddressU = CLAMP; AddressV = CLAMP;
};

static float2 ShadowmapOffset  = 0.5 / float2(SHADOW_MAP_WIDTH, SHADOW_MAP_HEIGHT);
static float2 ShadowmapOffset2 = 1.0 / float2(SHADOW_MAP_WIDTH, SHADOW_MAP_HEIGHT);

float ShadingShadow(MaterialParam material, float3 V)
{
	float3 viewPosition = V * material.linearDepth / V.z;

	float3 P = mul(float4(viewPosition, 1), matViewInverse).xyz;
	float3 L = P - LightPosition;

	float linearDepth = length(L);
	float depth = (mPointLightFar / linearDepth) * (linearDepth - mPointLightNear) / (mPointLightFar - mPointLightNear);

	float2 shadow = 0.0;
	float2 coord = CalcDualShadowCoord(L / linearDepth, SHADOW_MAP_HEIGHT);

	static const float radius = 3;
	static const float kernel[7] = {0.071303, 0.131514, 0.189879, 0.214607, 0.189879, 0.131514, 0.071303};

	[unroll]
	for (int x = -radius; x <= radius; x++) 
	{
		[unroll]
		for (int y = -radius; y <= radius; y++)
		{
			float2 pos = coord + float2(x, y) * ShadowmapOffset2;
			shadow += tex2Dlod(ShadowMapSamp, float4(pos, 0, 0)).rg * kernel[radius+x] * kernel[radius+y];
		}
	}

	return ChebyshevUpperBound(shadow, depth, 1e-4, LightHardness);
}
#endif

void ShadingMaterial(MaterialParam material, float3 V, out float3 diffuse, out float3 specular)
{
	float3 P = V * material.linearDepth / V.z;
	
	float3 L = viewLightPosition - P;
	float3 L2 = normalize(L);

	float3 atten = GetPhysicalLightAttenuation(L - L2 * LightSize, LightRange, LightAttenuationBulb);
	atten *= LightColor;

	float roughness = max(SmoothnessToRoughness(material.smoothness), 0.001);
	
	float4 energy = SphereAreaLightEnergy(material, material.normal, L, V, LightSize, roughness, material.specular);
	diffuse = energy.r + SubsurfaceShadingFast(material, material.normal, L2, V);
	diffuse *= atten;
	
#if LIGHTSOURCE_DIFFUSE_ONLY
	specular = 0;
#else
	specular = energy.gba;
	specular *= atten;
#endif
	
#if IBL_ENABLE
	float2 coord1 = ComputeSphereCoord(mul(L2, (float3x3)matViewInverse));
	diffuse *= DecodeHDR(tex2Dlod(IBLDiffuseSampler, float4(coord1, 0, 0)));

#if LIGHTSOURCE_DIFFUSE_ONLY == 0
	float3 Ls = mul(normalize(SphereLightDirection(material.normal, V, L, LightSize)), (float3x3)matViewInverse);
	float mipLayer = lerp(IBL_MIPMAP_LEVEL - 1, 0, material.smoothness * material.smoothness);
	specular *= DecodeHDR(tex2Dlod(IBLSpecularSampler, float4(ComputeSphereCoord(Ls), 0, mipLayer)));
#endif
#endif
}

void GetGbufferParams(float4 texcoord, out MaterialParam material, out MaterialParam materialAlpha)
{
	float2 coord = texcoord.xy / texcoord.w;

	float4 MRT5 = tex2Dlod(Gbuffer5Map, float4(coord, 0, 0));
	float4 MRT6 = tex2Dlod(Gbuffer6Map, float4(coord, 0, 0));
	float4 MRT7 = tex2Dlod(Gbuffer7Map, float4(coord, 0, 0));
	float4 MRT8 = tex2Dlod(Gbuffer8Map, float4(coord, 0, 0));

	DecodeGbuffer(MRT5, MRT6, MRT7, MRT8, materialAlpha);

	clip(sum(materialAlpha.albedo + materialAlpha.specular) - 1e-5);

	float4 MRT1 = tex2Dlod(Gbuffer1Map, float4(coord, 0, 0));
	float4 MRT2 = tex2Dlod(Gbuffer2Map, float4(coord, 0, 0));
	float4 MRT3 = tex2Dlod(Gbuffer3Map, float4(coord, 0, 0));
	float4 MRT4 = tex2Dlod(Gbuffer4Map, float4(coord, 0, 0));

	DecodeGbuffer(MRT1, MRT2, MRT3, MRT4, material);
}

void LightSourceVS(
	in float4 Position : POSITION,
	in float3 Normal : NORMAL,
	in float2 Texcoord : TEXCOORD0,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oTexcoord1 : TEXCOORD1,
	out float4 oPosition  : POSITION)
{
	Position.xyz = LightPosition + Normal * LightRange;
	oTexcoord1 = -mul(Position, matView);
	oTexcoord0 = oPosition = mul(Position, matViewProject);
	oTexcoord0.xy = PosToCoord(oTexcoord0.xy / oTexcoord0.w) + ViewportOffset;
	oTexcoord0.xy = oTexcoord0.xy * oTexcoord0.w;
}

void LightSourcePS(
	float4 texcoord : TEXCOORD0, 
	float3 viewdir : TEXCOORD1,
	out float4 oColor0 : COLOR0,
	out float4 oColor1 : COLOR1,
	out float4 oColor2 : COLOR2)
{
	MaterialParam material;
	MaterialParam materialAlpha;
	GetGbufferParams(texcoord, material, materialAlpha);

	float3 V = normalize(viewdir);
	
	float3 diffuse, specular;
	ShadingMaterial(material, V, diffuse, specular);
	
	float3 diffuseAlpha, specularAlpha;
	ShadingMaterial(materialAlpha, V, diffuseAlpha, specularAlpha);
	
#if SSSS_QUALITY
	oColor0 = float4(diffuse, 0);
#else
	oColor0 = float4(diffuse * material.albedo, 0);
#endif
	oColor1 = float4(specular, 0);
	oColor2 = float4(diffuseAlpha * materialAlpha.albedo + specularAlpha, 0);
	
#if SHADOW_MAP_ENABLE
	clip(sum(oColor0 + oColor1 + oColor2) - 1e-5);

	float shadow = ShadingShadow(materialAlpha, V);
	oColor0 *= shadow;
	oColor1 *= shadow;
	oColor2 *= shadow;
#endif
}

void LightSourceShadowlessPS(
	float4 texcoord : TEXCOORD0, 
	float3 viewdir : TEXCOORD1,
	out float4 oColor0 : COLOR0,
	out float4 oColor1 : COLOR1,
	out float4 oColor2 : COLOR2)
{
	MaterialParam material;
	MaterialParam materialAlpha;
	GetGbufferParams(texcoord, material, materialAlpha);

	float3 V = normalize(viewdir);
	
	float3 diffuse, specular;
	ShadingMaterial(material, V, diffuse, specular);
	
	float3 diffuseAlpha, specularAlpha;
	ShadingMaterial(materialAlpha, V, diffuseAlpha, specularAlpha);
	
#if SSSS_QUALITY
	oColor0 = float4(diffuse, 0);
#else
	oColor0 = float4(diffuse * material.albedo, 0);
#endif
	oColor1 = float4(specular, 0);
	oColor2 = float4(diffuseAlpha * materialAlpha.albedo + specularAlpha, 0);
}

technique MainTech0<string MMDPass = "object";
	string Script = 
		"RenderColorTarget0=;"
		"RenderColorTarget1=LightSpecMap;"
		"RenderColorTarget2=LightAlphaMap;"
		"Pass=DrawObject;"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = CW;
		VertexShader = compile vs_3_0 LightSourceVS();
		PixelShader  = compile ps_3_0 LightSourceShadowlessPS();
	}
}

technique MainTecBS0<string MMDPass = "object_ss";
	string Script = 
		"RenderColorTarget0=;"
		"RenderColorTarget1=LightSpecMap;"
		"RenderColorTarget2=LightAlphaMap;"
		"Pass=DrawObject;"
;>{
	pass DrawObject {
		ZEnable = false; ZWriteEnable = false;
		AlphaBlendEnable = TRUE; AlphaTestEnable = FALSE;
		SrcBlend = ONE; DestBlend = ONE;
		CullMode = CW;
		VertexShader = compile vs_3_0 LightSourceVS();
		PixelShader  = compile ps_3_0 LightSourcePS();
	}
}

technique EdgeTec<string MMDPass = "edge";>{}
technique ShadowTech<string MMDPass = "shadow";>{}
technique ZplotTec<string MMDPass = "zplot";>{}