#define C0_F0 1.624835
#define C0_EXP_MULT0 -0.862325
#define C0_REAL0 1.1793828124
#define C0_IM0 -0.7895320249

#define C1_F0 5.268909
#define C1_EXP_MULT0 -0.886528
#define C1_REAL0 -0.7406246191
#define C1_IM0 -0.3704940302

#define C1_F1 1.558213
#define C1_EXP_MULT1 -1.960518
#define C1_REAL1 1.5973700402
#define C1_IM1 -1.4276936105

#define DOF_BLUR_MODE 1
#define DOF_BLUR_RADIUS 12
#define DOF_BLUR_LENGTH (DOF_BLUR_RADIUS * 2)

static const float mFstop = lerp(lerp(2.8, 8.0, mFstopP), 1.0, mFstopM);
static const float mFocalLength = lerp(lerp(2.0, 100.0, mFocalLengthP), 1e-5, mFocalLengthM);
static const float mFocalDistance = lerp(lerp(1, 100.0, mFocalDistanceP), -10.0, mFocalDistanceM);
static const float mFocalScale = lerp(lerp(1.0, 5.0, mFocalScaleP), 0.5, mFocalScaleM);
static const float mFocalMapScale = 1;
static const float mFocalCircleRadius = 4;

static const float2 mFocalStepScale = 512 * float2(ViewportAspect, 1);

texture FocalBlurRMap : RENDERCOLORTARGET<float2 ViewportRatio={mFocalMapScale, mFocalMapScale}; string Format="A16B16G16R16F";>;
texture FocalBlurGMap : RENDERCOLORTARGET<float2 ViewportRatio={mFocalMapScale, mFocalMapScale}; string Format="A16B16G16R16F";>;
texture FocalBlurBMap : RENDERCOLORTARGET<float2 ViewportRatio={mFocalMapScale, mFocalMapScale}; string Format="A16B16G16R16F";>;
texture FocalBokehMap : RENDERCOLORTARGET<float2 ViewportRatio={mFocalMapScale, mFocalMapScale}; string Format="A16B16G16R16F";>;
texture FocalBokehFarMap : RENDERCOLORTARGET<float2 ViewportRatio={mFocalMapScale, mFocalMapScale}; string Format="A16B16G16R16F";>;
texture FocalBokehNearMap : RENDERCOLORTARGET<float2 ViewportRatio={mFocalMapScale, mFocalMapScale}; string Format="A16B16G16R16F";>;
texture FocalKernelMap : RENDERCOLORTARGET<int2 Dimensions={DOF_BLUR_LENGTH, 1}; string Format="A16B16G16R16F";>;

sampler FocalBlurRMapSamp = sampler_state { texture=<FocalBlurRMap>; MinFilter=LINEAR; MagFilter=LINEAR; MipFilter=NONE; AddressU=CLAMP; AddressV=CLAMP; };
sampler FocalBlurGMapSamp = sampler_state { texture=<FocalBlurGMap>; MinFilter=LINEAR; MagFilter=LINEAR; MipFilter=NONE; AddressU=CLAMP; AddressV=CLAMP; };
sampler FocalBlurBMapSamp = sampler_state { texture=<FocalBlurBMap>; MinFilter=LINEAR; MagFilter=LINEAR; MipFilter=NONE; AddressU=CLAMP; AddressV=CLAMP; };
sampler FocalBokehMapSamp = sampler_state { texture=<FocalBokehMap>; MinFilter=LINEAR; MagFilter=LINEAR; MipFilter=NONE; AddressU=CLAMP; AddressV=CLAMP; };
sampler FocalBokehMapPointSamp = sampler_state { texture=<FocalBokehMap>; MinFilter=POINT; MagFilter=POINT; MipFilter=NONE; AddressU=CLAMP; AddressV=CLAMP; };
sampler FocalBokehFarMapSamp = sampler_state { texture=<FocalBokehFarMap>; MinFilter=LINEAR; MagFilter=LINEAR; MipFilter=NONE; AddressU=CLAMP; AddressV=CLAMP; };
sampler FocalBokehNearMapSamp = sampler_state { texture=<FocalBokehNearMap>; MinFilter=LINEAR; MagFilter=LINEAR; MipFilter=NONE; AddressU=CLAMP; AddressV=CLAMP; };
sampler FocalKernelMapSamp = sampler_state { texture=<FocalKernelMap>; MinFilter=POINT; MagFilter=POINT; MipFilter=NONE; AddressU=CLAMP; AddressV=CLAMP; };

float GetFocalDistance()
{
	float focalDistance = step(0.5, mManualMode) ? mFocalDistance : distance(CameraPosition, AcsPosition) + mFocalDistance;
	return max(1, focalDistance);
}

float GetFocalAperture(float Fstop)
{
	float aperture = 1.0 / Fstop;
	return aperture;
}

float GetFocalLength(float mFocalLength, float focalDistance)
{
	float F = 0.5 * 35 * matProject._22;
	return F / (1.0 / mFocalLength + F / focalDistance);
}

float ComputeDepthCoC(float depth, float4 focalParams, float focalScale)
{
	float D = depth;
	float P = focalParams.x;
	float F = focalParams.y;
	float aspect = focalParams.z;

	P *= 0.001f / 100.0f;
	D *= 0.001f / 100.0f;
	F *= 0.001f / 100.0f;

	float CoC = aspect * F * (D - P) / (D * (P - F));

	return clamp(CoC * focalScale, -2, 3);
}

float EncodeCoC(float CoC)
{
	return CoC;
}

float DecodeCoC(float CoC)
{
	return CoC;
}

float4 ComplexKernel(in float x)
{
#if DOF_BLUR_MODE == 0
	float4 complexWeights = 0;
	complexWeights.x = (C0_REAL0 * cos(x*x*C0_F0) - C0_IM0   * sin(x*x*C0_F0)) * exp(C0_EXP_MULT0*x*x);
	complexWeights.y = (C0_IM0   * cos(x*x*C0_F0) + C0_REAL0 * sin(x*x*C0_F0)) * exp(C0_EXP_MULT0*x*x);
	return complexWeights;
#else
	float4 complexWeightsTwo = float4(
		(C1_REAL0* cos(x*x*C1_F0) - C1_IM0   * sin(x*x*C1_F0)) * exp(C1_EXP_MULT0*x*x),
		(C1_IM0  * cos(x*x*C1_F0) + C1_REAL0 * sin(x*x*C1_F0)) * exp(C1_EXP_MULT0*x*x),
		(C1_REAL1* cos(x*x*C1_F1) - C1_IM1   * sin(x*x*C1_F1)) * exp(C1_EXP_MULT1*x*x),
		(C1_IM1  * cos(x*x*C1_F1) + C1_REAL1 * sin(x*x*C1_F1)) * exp(C1_EXP_MULT1*x*x));
	return complexWeightsTwo;
#endif
}

float2 ComplexMultiply(in float2 a, in float2 b)
{
	return float2(a.x * b.x - a.y * b.y, a.x * b.y + b.x * a.y);
}

float4 ComputeDepthBokehVS(
	in float4 Position : POSITION,
	in float4 Texcoord : TEXCOORD,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oTexcoord1 : TEXCOORD1) : POSITION
{
	float focalDistance = GetFocalDistance();
	float focalLength = GetFocalLength(mFocalLength, focalDistance);
	float focalAperture = GetFocalAperture(mFstop);
	float4 focalCameraParams = float4(focalDistance, focalLength, focalAperture, 1);

	oTexcoord0 = Texcoord.xyxy;
	oTexcoord0.xy += ViewportOffset;
	oTexcoord1 = focalCameraParams;
	return Position;
}

float4 ComputeDepthBokehPS(in float2 coord : TEXCOORD0, in float4 focalCameraParams : TEXCOORD1, uniform sampler source) : COLOR
{
	float CoC = ComputeDepthCoC(tex2Dlod(Gbuffer8Map, float4(coord, 0, 0)).r, focalCameraParams, mFocalScale);
	return float4(tex2Dlod(source, float4(coord, 0, 0)).rgb, EncodeCoC(CoC));
}

float4 ComputeDepthBokeh4XPS(in float2 coord : TEXCOORD0, in float4 focalCameraParams : TEXCOORD1, uniform sampler source) : COLOR
{
	float4 coords[4];
	coords[0] = float4(coord, 0, 0);
	coords[1] = float4(coord + float2(ViewportOffset2.x, 0), 0, 0);
	coords[2] = float4(coord + float2(0, ViewportOffset2.y), 0, 0);
	coords[3] = float4(coord + float2(ViewportOffset2.x, ViewportOffset2.y), 0, 0);

	float4 linearDepths;
	linearDepths.x = tex2Dlod(Gbuffer8Map, coords[0]).r;
	linearDepths.y = tex2Dlod(Gbuffer8Map, coords[1]).r;
	linearDepths.z = tex2Dlod(Gbuffer8Map, coords[2]).r;
	linearDepths.w = tex2Dlod(Gbuffer8Map, coords[3]).r;

	float3 colors[4];
	colors[0] = tex2Dlod(source, coords[0]).rgb;
	colors[1] = tex2Dlod(source, coords[1]).rgb;
	colors[2] = tex2Dlod(source, coords[2]).rgb;
	colors[3] = tex2Dlod(source, coords[3]).rgb;

	float4 CoC;
	CoC.x = ComputeDepthCoC(linearDepths.x, focalCameraParams, mFocalScale);
	CoC.y = ComputeDepthCoC(linearDepths.y, focalCameraParams, mFocalScale);
	CoC.z = ComputeDepthCoC(linearDepths.z, focalCameraParams, mFocalScale);
	CoC.w = ComputeDepthCoC(linearDepths.w, focalCameraParams, mFocalScale);

	float4 color = 0;
	color.rgb += colors[0] * abs(CoC.x);
	color.rgb += colors[1] * abs(CoC.y);
	color.rgb += colors[2] * abs(CoC.z);
	color.rgb += colors[3] * abs(CoC.w);
	color.rgb /= dot(abs(CoC), 1.0f);

	color.a = CoC.x;
	if(abs(color.a) > CoC.y) color.a = CoC.y;
	if(abs(color.a) > CoC.z) color.a = CoC.z;
	if(abs(color.a) > CoC.w) color.a = CoC.w;
	if(color.a > 0)	color.a = dot(0.25f, max(0, CoC));
	color.a = EncodeCoC(color.a);

	return min(color, float4(65535, 65535, 65535, 65535));
}

float4 ComplexKernelVS(
	in float4 Position : POSITION,
	in float4 Texcoord : TEXCOORD,
	out float4 oTexcoord0 : TEXCOORD0) : POSITION
{
	oTexcoord0 = Texcoord.xyxy;
	return Position;
}

float4 ComplexKernelPS(in float2 coord : TEXCOORD0) : COLOR
{
	float4 kernel = ComplexKernel(coord.x * 2 - 1);
	return kernel;
}

float4 ComputeComplexVS(
	in float4 Position : POSITION,
	in float4 Texcoord : TEXCOORD,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oTexcoord1 : TEXCOORD1) : POSITION
{
	oTexcoord0 = Texcoord.xyxy;
	oTexcoord0.xy += 0.5 / (ViewportSize * mFocalMapScale);
	oTexcoord0.zw = oTexcoord0.zw * ViewportSize;

	oTexcoord1 = ComplexKernel(0);

	for (int i = 1; i <= DOF_BLUR_RADIUS; ++i)
		oTexcoord1 += ComplexKernel(float(i) / DOF_BLUR_RADIUS) * 2;

	return Position;
}

void ComputeComplexXPS(
	in float2 coord : TEXCOORD0, 
	out float4 oColor0 : COLOR0,
	out float4 oColor1 : COLOR1,
	out float4 oColor2 : COLOR2,
	uniform sampler bokeh,
	uniform sampler source)
{
	float CoC = DecodeCoC(tex2Dlod(bokeh, float4(coord, 0, 0)).a);
	clip(CoC);

	float2 offset = float2(mFocalCircleRadius / mFocalStepScale.x, 0.0) * CoC / DOF_BLUR_RADIUS;

	float4 complexR = 0.0, complexG = 0.0, complexB = 0.0;

	float2 complexCoord = coord - DOF_BLUR_RADIUS * offset;
	float2 complexKernelCoord = 0.5 / float2(DOF_BLUR_LENGTH , 2);

	for (int i = -DOF_BLUR_RADIUS; i <= DOF_BLUR_RADIUS; ++i, complexCoord += offset, complexKernelCoord.x += (1.0 / DOF_BLUR_LENGTH))
	{
		float4 samp = tex2Dlod(source, float4(complexCoord, 0, 0));
		samp.a = DecodeCoC(samp.a);

		if (samp.a > 0)
		{
			float4 kernel = tex2Dlod(FocalKernelMapSamp, float4(complexKernelCoord.x, 0, 0, 0)) * samp.a;

			complexR.xy += ComplexMultiply(float2(samp.r, 0.0), kernel.xy);
			complexR.zw += ComplexMultiply(float2(samp.r, 0.0), kernel.zw);

			complexG.xy += ComplexMultiply(float2(samp.g, 0.0), kernel.xy);
			complexG.zw += ComplexMultiply(float2(samp.g, 0.0), kernel.zw);

			complexB.xy += ComplexMultiply(float2(samp.b, 0.0), kernel.xy);
			complexB.zw += ComplexMultiply(float2(samp.b, 0.0), kernel.zw);
		}
	}

	oColor0 = complexR;
	oColor1 = complexG;
	oColor2 = complexB;
}

float4 ComputeComplexYPS(in float2 coord : TEXCOORD0, in float4 kernelAccum : TEXCOORD1, uniform sampler source) : COLOR
{
	float CoC = DecodeCoC(tex2Dlod(source, float4(coord, 0, 0)).a);
	clip(CoC);

	float2 offset = float2(0, mFocalCircleRadius / mFocalStepScale.y) * CoC / DOF_BLUR_RADIUS;

	float complexWeight = 0;

	float3 complexColor = 0.0;
	float2 complexCoord = coord - DOF_BLUR_RADIUS * offset;
	float2 complexKernelCoord = 0.5 / float2(DOF_BLUR_LENGTH , 1);

	for (int i = -DOF_BLUR_RADIUS; i <= DOF_BLUR_RADIUS; ++i, complexCoord += offset, complexKernelCoord.x += (1.0 / DOF_BLUR_LENGTH))
	{
		float4 samp0 = tex2Dlod(source, float4(complexCoord, 0, 0));
		samp0.a = DecodeCoC(samp0.a);

		if (samp0.a > 0)
		{
			float4 samp1 = tex2Dlod(FocalBlurRMapSamp, float4(complexCoord, 0, 0));
			float4 samp2 = tex2Dlod(FocalBlurGMapSamp, float4(complexCoord, 0, 0));
			float4 samp3 = tex2Dlod(FocalBlurBMapSamp, float4(complexCoord, 0, 0));

			float4 sampKernel = tex2Dlod(FocalKernelMapSamp, float4(complexKernelCoord.x, 0, 0, 0)) * samp0.a;

			complexColor.r += ComplexMultiply(samp1.xy, sampKernel.xy).x;
			complexColor.r += ComplexMultiply(samp1.zw, sampKernel.zw).x;

			complexColor.g += ComplexMultiply(samp2.xy, sampKernel.xy).x;
			complexColor.g += ComplexMultiply(samp2.zw, sampKernel.zw).x;

			complexColor.b += ComplexMultiply(samp3.xy, sampKernel.xy).x;
			complexColor.b += ComplexMultiply(samp3.zw, sampKernel.zw).x;

			complexWeight += ComplexMultiply(sampKernel.xy, kernelAccum.xy * samp0.a).x;
			complexWeight += ComplexMultiply(sampKernel.zw, kernelAccum.zw * samp0.a).x;
		}
	}

	return float4(complexColor / complexWeight, EncodeCoC(CoC));
}

float4 GaussianBlurNearPS(in float2 coord : TEXCOORD0, uniform sampler bokeh, uniform sampler source, uniform float2 offset) : COLOR
{
	float weights[15] = {0.0001f, 0.0009f, 0.0047f, 0.0188f, 0.0553f, 0.1196f, 0.1899f, 0.2216f, 0.1899f, 0.1196f, 0.0553f, 0.0188f, 0.0047f, 0.0009f, 0.0001f};

	float CoC = tex2Dlod(bokeh, float4(coord, 0, 0)).a;

	offset *= abs(DecodeCoC(CoC));

	coord -= offset * 7;

	float4 color = 0;

	[unroll]
	for(int i = 0; i < 15; i++, coord += offset)
	{
		float4 color1 = tex2Dlod(source, float4(coord,0,0));
		float weight = weights[i] * max(0.1, abs(DecodeCoC(color1.a)));

		color.rgb += color1.rgb * weight;
		color.a += weight;
	}

	return float4(color.rgb / color.a, CoC);
}

float4 ComputeBokehGatherVS(
	in float4 Position : POSITION,
	in float4 Texcoord : TEXCOORD,
	out float4 oTexcoord0 : TEXCOORD0,
	out float4 oTexcoord1 : TEXCOORD1) : POSITION
{
	float focalDistance = GetFocalDistance();
	float focalLength = GetFocalLength(mFocalLength, focalDistance);
	float focalAperture = GetFocalAperture(mFstop);
	float4 focalCameraParams = float4(focalDistance, focalLength, focalAperture, 1);

	oTexcoord0 = Texcoord.xyxy;
	oTexcoord0.xy += ViewportOffset;
	oTexcoord0.zw += float2(0.5, 0.5) / (ViewportSize * mFocalMapScale);
	oTexcoord1 = focalCameraParams;
	return Position;
}

float4 ComputeBokehGatherPS(in float4 coord : TEXCOORD0, in float4 focalCameraParams : TEXCOORD1) : COLOR
{
	float4 coords[4];
	coords[0] = float4(coord.zw, 0, 0);
	coords[1] = float4(coord.zw + float2(1.0, 0.0) / (ViewportSize * mFocalMapScale), 0, 0);
	coords[2] = float4(coord.zw + float2(0.0, 1.0) / (ViewportSize * mFocalMapScale), 0, 0);
	coords[3] = float4(coord.zw + float2(1.0, 1.0) / (ViewportSize * mFocalMapScale), 0, 0);

	float4 samp1 = tex2Dlod(FocalBokehFarMapSamp, coords[0]);
	float4 samp2 = tex2Dlod(FocalBokehNearMapSamp, coords[0]);

	float CoC = ComputeDepthCoC(tex2Dlod(Gbuffer8Map, float4(coord.xy, 0, 0)).r, focalCameraParams, mFocalScale);
	float CoCalpha = CoC * 2;

	float4 color;
	color.rgb = lerp(samp2.rgb, samp1.rgb, saturate(CoCalpha));
	color.rgb = lerp(color, ((CoC.x > 0) ? float3(0,0.05,0.1) : float3(0.1,0.05,0)) * abs(CoC.x), mVisualizationMode);
	color.a = lerp(saturate(pow2(CoC * 2)), 1, mVisualizationMode);

	return color;
}