float4 HDRTonemappingVS(
	in float4 Position : POSITION,
	in float4 Texcoord : TEXCOORD,
	out float4 oTexcoord : TEXCOORD0) : POSITION
{
	oTexcoord = Texcoord;
	oTexcoord.xy += ViewportOffset;
	oTexcoord.zw = mExposure;
#if HDR_EYE_ADAPTATION == 1
	float aveLum = tex2Dlod(EyeLumAveMapSamp, float4(0.5,0.5,0,0)).r;
	oTexcoord.zw *= ComputeExposureISO(aveLum, 12.5);
#elif HDR_EYE_ADAPTATION == 2
	float aveLum = tex2Dlod(EyeLumAveMapSamp, float4(0.5,0.5,0,0)).r;
	oTexcoord.zw *= ComputeExposureISO(aveLum, 18.0);
#endif
	return Position;
}

float4 HDRTonemappingPS(in float4 coord: TEXCOORD0, uniform sampler source) : COLOR
{
	float3 color = tex2Dlod(source, float4(coord.xy, 0, 0)).rgb;

	color *= coord.z;

#if HDR_BLOOM_MODE
	float3 bloom = tex2Dlod(BloomSamp1st, float4(coord.xy, 0, 0)).rgb;
	color += bloom;
#endif

	color = ColorTemperature(color, mColorTemperature);
	color = ColorCorrect(color, mColorSaturation, mColorContrast, mColorGamma, mColorBalanceP, mColorBalanceM);
	color = ColorToneMapping(color);

	color = linear2srgb(color);
	color = ColorDithering(color, coord.xy);

#if AA_QUALITY
	return float4(color, luminance(color));
#else
	return float4(color, 1);
#endif
}