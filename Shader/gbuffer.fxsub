#define SHADINGMODELID_DEFAULT      0
#define SHADINGMODELID_SKIN         1
#define SHADINGMODELID_EMISSIVE     2
#define SHADINGMODELID_HAIR         3
#define SHADINGMODELID_EYE          4
#define SHADINGMODELID_CLOTH        5
#define SHADINGMODELID_CLEAR_COAT   6
#define SHADINGMODELID_SUBSURFACE   7
#define SHADINGMODELID_MASK         8

#define LIGHTINGMODEL_NORMAL         0
#define LIGHTINGMODEL_TRANSMITTANCE  1
#define LIGHTINGMODEL_EMISSIVE       2

#define MAX_FRACTIONAL_8_BIT        (255.0f / 256.0f)
#define TWO_BITS_EXTRACTION_FACTOR  (3.0f + MAX_FRACTIONAL_8_BIT)

struct MaterialParam
{
	float3 normal;
	float3 albedo;
	float3 specular;
	float3 emissive;
	float emissiveIntensity;
	float smoothness;
	float alpha;
	float customDataA;
	float3 customDataB;
	float linearDepth;
	int lightModel;
};

struct GbufferParam
{
	float4 buffer1 : COLOR0;
	float4 buffer2 : COLOR1;
	float4 buffer3 : COLOR2;
	float4 buffer4 : COLOR3;
};

float3 EncodeNormal(float3 normal)
{
	float p = sqrt(-normal.z * 8 + 8);
	float2 enc = normal.xy / p + 0.5f;
	float2 enc255 = enc * 255;
	float2 residual = floor(frac(enc255) * 16);
	return float3(floor(enc255), residual.x * 16 + residual.y) / 255;
}

float3 DecodeNormal(float3 enc)
{
	float nz = floor(enc.z * 255) / 16;
	enc.xy += float2(floor(nz) / 16, frac(nz)) / 255;
	float2 fenc = enc.xy * 4 - 2;
	float f = dot(fenc, fenc);
	float g = sqrt(1 - f / 4);
	float3 normal;
	normal.xy = fenc * g;
	normal.z = f / 2 - 1;
	return normalize(normal);
}

void DecodeGbuffer(float4 buffer1, float4 buffer2, float4 buffer3, float4 buffer4, out MaterialParam material)
{
	material.albedo = buffer1.xyz;
	material.smoothness = buffer1.w;

	material.normal = DecodeNormal(buffer2.xyz);
	material.emissive = 0;
	material.emissiveIntensity = 0;
	
	material.customDataA = buffer2.w;
	material.customDataB = 0;
	
	material.linearDepth = buffer4.x;
	material.alpha = buffer4.w;
	
	material.lightModel = (int)floor(buffer3.w * TWO_BITS_EXTRACTION_FACTOR);
	if (material.lightModel == LIGHTINGMODEL_TRANSMITTANCE)
	{
		material.lightModel = floor(buffer2.w * TWO_BITS_EXTRACTION_FACTOR) < 1.0 ? SHADINGMODELID_SUBSURFACE : SHADINGMODELID_SKIN;
	}
	
	if (buffer4.z <= 1e-5)
	{
		material.lightModel = (int)floor(abs(buffer4.z));
	}
	
	if (material.lightModel == SHADINGMODELID_SKIN || 
		material.lightModel == SHADINGMODELID_SUBSURFACE ||
		material.lightModel == SHADINGMODELID_CLOTH)
	{
		material.specular = buffer3.xxx;
		material.customDataB = ycbcr2rgb(float3(frac(buffer3.w * TWO_BITS_EXTRACTION_FACTOR), buffer3.yz));
	}
	else if (material.lightModel == SHADINGMODELID_EMISSIVE)
	{
		material.specular = buffer3.xxx;
		material.emissive = ycbcr2rgb(float3(frac(buffer3.w * TWO_BITS_EXTRACTION_FACTOR), buffer3.yz));
		material.emissiveIntensity = buffer4.y;
	}
	else
	{
		material.specular = ycbcr2rgb(buffer3.xyz);
	}
}

float3 DecodeGBufferNormal(float4 buffer2)
{
	return DecodeNormal(buffer2.rgb);
}

float3 DecodeGBufferEmissive(float4 buffer3, float4 buffer4)
{
	int lightModel = buffer4.z <= 1e-5 ? (int)floor(abs(buffer4.z)) : (int)floor(buffer3.w * TWO_BITS_EXTRACTION_FACTOR);   
	if (lightModel == SHADINGMODELID_EMISSIVE)
		return max(0, ycbcr2rgb(float3(frac(buffer3.w * TWO_BITS_EXTRACTION_FACTOR), buffer3.yz)) * buffer4.y);
	else
		return 0;
}

float3 ReconstructPos(float2 Tex, float4x4 matProjectInverse, float depth)
{
	float3 v = mul(float4(CoordToPos(Tex), 0, 1), matProjectInverse).xyz;
	return v * depth / v.z;
}

float4 EncodeYcbcr(float4 screenPosition, float3 color1, float3 color2)
{
	bool pattern = (fmod(screenPosition.x, 2.0) == fmod(screenPosition.y, 2.0));

	color1 = rgb2ycbcr(color1);
	color2 = rgb2ycbcr(color2);

	float4 result = 0.0f;
	result.r = color1.r;
	result.g = (pattern) ? color1.g: color1.b;
	result.b = color2.r;
	result.a = (pattern) ? color2.g: color2.b;
	return result;   
}

float EdgeFilter(float2 center, float2 a0, float2 a1, float2 a2, float2 a3)
{
	const float THRESH = 30./255.;
	float4 lum = float4(a0.x, a1.x , a2.x, a3.x);
	float4 w = 1.0 - step(THRESH, abs(lum - center.x));
	float W = w.x + w.y + w.z + w.w;
	w.x = (W == 0.0) ? 1.0 : w.x;
	W   = (W == 0.0) ? 1.0 : W;
	return (w.x * a0.y + w.y * a1.y + w.z * a2.y + w.w * a3.y) / W;
}

void DecodeYcbcr(sampler source, float2 coord, float4 screenPosition, float2 offset, out float3 color1, out float3 color2)
{
	float4 packed = tex2Dlod(source, float4(coord, 0, 0));
	
	float4 env2 = tex2Dlod(source, float4(coord + float2(offset.x, 0.0), 0, 0));
	float4 env3 = tex2Dlod(source, float4(coord - float2(offset.x, 0.0), 0, 0));
	float4 env4 = tex2Dlod(source, float4(coord + float2(0.0, offset.y), 0, 0));
	float4 env5 = tex2Dlod(source, float4(coord - float2(0.0, offset.y), 0, 0));
	
	env2.rg = EdgeFilter(packed.rg, env2.rg, env3.rg, env4.rg, env5.rg);
	env2.ba = EdgeFilter(packed.ba, env2.ba, env3.ba, env4.ba, env5.ba);

	bool pattern = (fmod(screenPosition.x, 2.0) == fmod(screenPosition.y, 2.0));
	
	color1 = (pattern) ? float3(packed.rg, env2.g) : float3(packed.r, env2.g, packed.g);
	color2 = (pattern) ? float3(packed.ba, env2.a) : float3(packed.b, env2.a, packed.a);

	color1 = ycbcr2rgb(color1);
	color2 = ycbcr2rgb(color2);
}